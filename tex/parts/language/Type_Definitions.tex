%!TEX TS-program = xelatex
%!TEX encoding = UTF-8 Unicode

\chapter{Type Definitions}

\minitoc

\newpage

\section{Type Definitions}
\label{sec:type-decls-aliases}

\grammar\begin{lstlisting}
Def ::= ...
      | Type_Definition
Type_Definition 
    ::= 'type' Typedef {'and' Typedef} 'end' ['type']
Typedef 
    ::= [Type_Context] id [Type_Param_Clause] [Given_Clauses] [Type_Information]
      | [Type_Context] Prefix_Type_Param_Clause id [Given_Clauses] [Type_Information]
Type_Information
    ::= [Type_Equation [Type_Re-export] | Type_Abstract]
      | [Type_Representation]
Type_Equation 
    ::= '=' ['alias'] Type
Type_Abstract 
    ::= ['>:' Type] ['<:' Type] {'<%' Type} {':' Type}
Type_Re-export
    ::= '=' '(' 'include' Long_id ')'
\end{lstlisting}

A {\em type declaration} \lstinline!type $t$[$\tps$] >: $L$ <: $U$! declares $t$ to be an abstract type with lower bound type $L$ and upper bound type $U$. If the type parameter clause \lstinline@[$\tps$]@ is omitted, $t$ abstracts over a first-order type, otherwise $t$ stands for a type constructor that accepts type arguments as described by the type parameter clause. 

If a type declaration appears as a member declaration of a type, implementations of the type may implement $t$ with any type $T$, for which $L \conforms T \conforms U$. It is an error if $L$ does not conform to $U$. Either or both bounds may be omitted. If the lower bound $L$ is omitted, the bottom type \code{Nothing} is implied. If the upper bound $U$ is omitted, the top type \code{Object} is implied. 

A type constructor declaration declaration imposes additional restriction on the concrete types for which $t$ may stand. Besides the bounds $L$ and $U$, the type parameter clause, indexing parameter clause and units of measure parameter clause may impose higher-order bounds and variances, as governed by the conformance of type constructors (\sref{sec:conformance}).

The scope of a type parameter extends over the bounds ~\lstinline!>: $L$ <: $U$!, the type parameter clause $\tps$ itself and any type parameter clauses that $\tps$ is nested in.

A {\em type alias} \lstinline@type $t$ = $T$@ defines $t$ to be an alias name for the type $T$. Since---for type safety and consistence reasons---types are constant and can not be replaced by another type when bound to a constant name, type aliases are permanent. A type remembers the first given constant name, no alias can change that. The left hand side of a type alias may have a type parameter clause, e.g. \lstinline@type $t$[$\tps$] = $T$@. The scope of a type parameter extends over to the right hand side $T$ and the type parameter clause $\tps$ itself. 

It is an error if a type alias refers recursively to the defined type constructor itself. 

\example The following are legal type declarations and aliases:
\begin{lstlisting}
type Integer_List = List[Integer] end
type T <: Comparable[T] end
type Two['a] = Tuple_2['a, 'a] end
type My_Collection[+'x] <: Iterable['x] end
\end{lstlisting}

The following are illegal:
\begin{lstlisting}
type Abs = Comparable[Abs] end (* recursive type alias *)

type S <: T end  (* S, T are bounded by themselves *)
type T <: S end

type T >: Comparable[T.That] end (* can't select from T,
                                    T is an abstract type *)
type My_Collection <: Iterable end
  (* type constructor members must explicitly state
     their type parameters *)
\end{lstlisting}





\section{Aspect Definitions}
\label{sec:aspects}

\grammar\begin{lstlisting}
Type_Definition 
    ::= ...
      | Aspect_Definition
Aspect_Definition 
    ::= 'aspect' [Aspect_Clause] Aspect_Def
Aspect_Def
    ::= id ['extends' Class_Parents] Aspect_Tmpl_Env
Aspect_Tmpl_Env 
    ::= '{' Aspect_Body '}'
      | 'begin' Aspect_Body 'end' ['aspect']
Aspect_Body
    ::= Aspect_Stat {semi Aspect_Stat}
Aspect_Stat 
    ::= Advice_Def
      | Pointcut_Def
      | Template_Stat
Pointcut_Def 
    ::= 'pointcut' id
        ['(' [id [':' Type]] {',' [id [':' Type]]} ')']
        '=' Pointcut_Type 'end' ['pointcut']
Pointcut_Type 
    ::= 'invoke' (id | regular_expression_literal)
      | 'get' (id | regular_expression_literal)
      | 'set' (id | regular_expression_literal)
      | 'handler' Type
      | 'returning' [Id_Or_Type]
      | 'signalling' [Id_Or_Type]
      | 'throwing' [Id_Or_Type]
      | 'raising' [Id_Or_Type]
      | 'yielding' [Id_Or_Type]
      | 'advice-execution' [(id | regular_expression_literal)]
      | 'self' (id | Type)
      | 'target' (id | Type)
      | 'arguments' {'(' Id_Or_Type {',' Id_Or_Type} ')'}+
      | 'arguments' '(' ')'
      | ('if' | 'unless') '(' Expr ')'
      | 'not' Pointcut_Ref
      | Pointcut_Ref 'and' Pointcut_Ref
      | Pointcut_Ref 'or' Pointcut_Ref
      | '(' Pointcut_Ref ')'
      | 'retain' [Id_Or_Type]
      | 'release' [Id_Or_Type]
Pointcut_Ref 
    ::= id ['(' id {',' id} ')'] 
      | Pointcut_Type
Advice_Def 
    ::= 'advice' Advice_Spec (Pointcut_Ref | Pointcut_Type) Block_Argument
Id_Or_Type
    ::= id [':' Type] 
      | Type
Advice_Spec 
    ::= 'before' 
      | 'after' 
        [('returning' | 'throwing' | 'raising' | 'yielding' | 'signalling') 
         [Id_Or_Type]]
      | 'around'
Aspect_Clause
    ::= 'per-self' [Pointcut_Ref] 
      | 'per-target' [Pointcut_Ref] 
\end{lstlisting}

Only for \code{Block_Argument} inside of \code{Advice_Def}:
\begin{lstlisting}
Simple_Expr       ::= 'joinpoint' ['.' Selection]
\end{lstlisting}

% https://eclipse.org/aspectj/doc/released/progguide/semantics-pointcuts.html
% https://eclipse.org/aspectj/doc/released/progguide/quick.html (better)
% important term: published join point context data (args, target, caller etc.)
% also important: http://en.wikipedia.org/wiki/Cross-cutting_concern#Examples - see memory management (possible join points: allocation, deallocation, retain, release)







\section{Enumeration Type Definitions}
\label{sec:enums}

\grammar\begin{lstlisting}
Type_Representation
    ::= '=' 'enum' Enum_Field {semi Enum_Field}
        [Record_Extensions]
Enum_Field 
    ::= {Annotation} 'case' id ['=' Expr]
\end{lstlisting}

Enums (short for Enumerations) are types that contain constants.

An enum constant can be enhanced with custom parameters, which are then passed to the appropriate custom constructor. Enum definitions should only appear in three forms, called {\em constant enum constructors}
\begin{itemize}
  \item Initialized with a constant. Each enum constant then have a method \code{value}, which corresponds to the passed literal. Each literal passed has to be of the same type, and numeric and character literals get an auto-incremented value for every following enum constant definition that omits its explicit value. At least the first enum constant has to be initialized with a scalar literal. The comparison operators (e.g. ``\code{<}'') are automatically added and their implementations reflect the order in which the enum constants appear.
  \item Initialized with nothing, then each enum constant is by itself the ordering, no implicit literal value is added. The comparison operators are automatically added and their implementations reflect the order in which the enum constants appear. 
  \item Initialized with custom expression. The argument expression can be extracted with pattern matching as a contained single value, or if the argument expression is a tuple, as a tuple. It is highly recommended for every enum constant to be immutable, but it is not mandatory. This form can't extend any parent class and has the same implicit ordering as the previous form. 
\end{itemize}





\section{Variant Type Definitions}
\label{sec:variant-types}

\grammar\begin{lstlisting}
Type_Representation
    ::= ...
      | Variant_Type_Def
Variant_Type_Def
    ::= '=' 'variant' [nl] (Variant_Type_Representation) 
        [Record_Extensions]
Variant_Type_Representation 
    ::= Variant_Field {semi Variant_Field}
Variant_Field
    ::= Type_Constr_Dcl
Type_Constr_Dcl
    ::= {Annotation} 'case' id ['of' Type_Constr_Params]
Type_Constr_Params
    ::= Types
\end{lstlisting}

Variant types are similar to a simplified form for a series of case classes (\sref{sec:case-classes}) and case objects (\sref{sec:case-objects}), with shared interface and a common ancestor. Pattern matching is possible on variant types, as it would be done on case classes and case objects. 

Variant type is the algebraic sum type. Unlike unions (\sref{sec:unions}), variant type is a disjoint union -- its values are pairwise disjoint. The contained values however need not be pairwise disjoint. 

Unlike enumerations (\sref{sec:enums}), variant types are not just constants, they instead define classes of values. 

The form ~\lstinline!$\id$ of $T$! is called {\em non-constant variant constructors}. The objects that represent non-constant variant constructors can be memoized by the runtime, so that there is at most one singleton object per every arguments combination, but the runtime does not need to guarantee that such an object will be physically identical at different times. The contained values can be extracted with pattern matching, similar to enumerations, using tuple patterns (\sref{sec:tuple-patterns}). 

\paragraph{Note}
Non-constant variant constructors are internally type-parameterized with the same type parameters as the variant type. 

The form ~\lstinline!$\id$! is called {\em constant variant constructors}. The objects that represent constant variant constructors are singletons. Unlike with regular union types in Aml, constant variant constructors are not pre-existing types, but rather new types. 

Variant types may be parameterized, therefore polymorphic. Overloading on type parameters is possible. 

Record extension members, if given, are added to the type object, unless they are specified with \code{member} keyword, then they are added to the type instances. Implementations and declarations of interfaces are automatically added to the type instances. 

\example An example of a variant type. 
\begin{lstlisting}
type B-tree['t] = variant 
  case Empty
  case Node of 't * B-tree['t] * B-tree['t]
  with member is_member? x b-tree =
    match b-tree
    when Empty then no
    when Node y, left, right then
      if x = y then yes
      elsif x < y then is_member? x left
      else is_member? x right
      end if
    end match
  and member insert x b-tree = 
    match b-tree
    when Empty then Node x, Empty, Empty
    when Node y, left, right then
      if x <= y then Node y, (insert x left), right
      else Node y, left, (insert x right)
      end if
    end match
end type
\end{lstlisting}

Unlike enumeration types (\sref{sec:enums}), variant types do not enumerate a limited set of values, but instead represent classes of values with non-constant variant constructors and enumerate a limited set of singleton values with constant variant constructors\footnote{Thus a variant type that has only constant variant constructors is identical to an enumeration type, which is a situation to be avoided.}. 





\subsection{Generalized Algebraic Datatype Definitions}
\label{sec:gadt-types}

\grammar\begin{lstlisting}
Variant_Type_Def 
    ::= ...
      | '=' 'variant' [nl] (Gadt_Type_Representation) 
        [Record_Extensions]
Gadt_Type_Representation 
    ::= Gadt_Field {semi Gadt_Field}
Gadt_Field 
    ::= {Annotation} 'case' id ':' [Types '->'] Type  
        (* ':' instead of 'of' is the important distinction here *)
\end{lstlisting}







\subsection{Extensible Variant Types}
\label{sec:extensible-variant-types}

\grammar\begin{lstlisting}
Type_Representation 
    ::= ...
      | '=' 'open' 'variant' [nl] [Variant_Type_Representation] 
        [Record_Extensions]
Struct_Spec
    ::= ...
      | 'type' id [Type_Param_Clause] 
        '+=' Type_Constr_Dcl {semi Type_Constr_Dcl}
Struct_Def
    ::= ...
      | 'type' id [Type_Param_Clause] 
        '+=' Type_Constr_Def {semi Type_Constr_Def}
Type_Constr_Def
    ::= Type_Constr_Dcl 
      | 'case' id '=' Long_Id
\end{lstlisting}






\section{Record Type Definitions}
\label{sec:record-types}

\grammar\begin{lstlisting}
Type_Representation 
    ::= '=' 'record' Record_Type_Representation
Record_Type_Representation 
    ::= Record_Components 
        [Record_Extensions]
Record_Components 
    ::= '{' Record_Component {semi Record_Component} '}'
Record_Component 
    ::= {Annotation} {Record_Modifier} ['val' | 'var'] id ':' Type
Record_Extensions 
    ::= [nl] 'with' Record_Extension {'and' Record_Extension}
Record_Extension 
    ::= Tmpl_Member
      | Tmpl_Ifc_Impl
      | Tmpl_Ifc_Dcl
      | {Annotation}+ Record_Extension
Record_Modifier 
    ::= Access_Modifier 
      | Mutability
Type_Constr_Params
    ::= ...
      | Record_Components
\end{lstlisting}





\section[Structure, Signature \& Functor Type Definitions]{Structure, Signature \\\& Functor Type Definitions}
\label{sec:struct-types}

\grammar\begin{lstlisting}
Struct_Type 
    ::= Struct_Type_Path
      | 'sig' [Struct_Spec {semi Struct_Spec}] 'end'
      | 'functor' {'(' [id ':' Struct_Type] ')'}+ '->' Struct_Type
      | Struct_Type {'->' Struct_Type}+
      | Struct_Type 'with' Struct_Constraint {'and' Struct_Constraint}
      | '(' Struct_Type ')'
      | 'signature' 'of' Struct_Expr
Struct_Spec 
    ::= 'val' id [Type_Param_Clause] ':' Type
      | Type_Definition  (* containing Type_Abstract, 
                            or type class/family declaration *)
      | Protocol_Definition
      | 'structure' id ':' Struct_Type 'end' ['structure']
      | 'signature' id ['=' Struct_Type] 'end' ['signature']
      | 'structure' 'rec' id ':' Struct_Type 
        {'and' id ':' Struct_Type}+ 'end' ['structure']
      | ['public'] 'open' Struct_Path
      | ['public'] 'open!' Struct_Path
      | ['public'] Use_Clause
      | 'include' Struct_Type
      | {Annotation}+ Struct_Spec
Struct_Expr 
    ::= Struct_Path
      | 'struct' [Struct_Items] 'end' ['struct']
      | 'struct' '{' [Struct_Items] '}'
      | 'functor' {'(' [id ':' Struct_Type] ')'}+ '->' Struct_Expr
      | Struct_Expr {'(' [Struct_Expr] ')'}+
      | '(' Struct_Expr ')'
      | '(' Struct_Expr 'as' Struct_Type ')'
      | '(' Struct_Expr Struct_Ascription ')'
      | '(' 'val' Expr ['as' Pkg_Type] ')'
Struct_Items 
    ::= Struct_Item {semi Struct_Item} 
Struct_Item 
    ::= Struct_Def 
      | Do_Binding
Struct_Def 
    ::= [Access_Modifier] Val_Def
      | [Access_Modifier] Type_Definition  (* without Type_Abstract *)
      | {Modifier} Tmpl_Def
      | [Access_Modifier] 'structure' id [Struct_Ascription] 
        '=' Struct_Expr
      | [Access_Modifier] 'structure' 'rec' id [Struct_Ascription] 
        '=' Struct_Expr 
        {'and' id [Struct_Ascription] '=' Struct_Expr}
      | ['public'] 'open' Struct_Path
      | ['public'] 'open!' Struct_Path
      | ['public'] Use_Clause
      | 'include' Struct_Expr
      | {Annotation}+ Struct_Def
Struct_Ascription 
    ::= ':' Struct_Type
      | ':>' Struct_Type
Struct_Constraint 
    ::= 'type' Simple_Type Type_Equation
      | 'struct' Struct_Path '=' Extended_Struct_Path
        (* substituting inside signature: *)
      | 'type' Simple_Type ':=' Type
      | 'struct' id ':=' Extended_Struct_Path
Simple_Type 
    ::= ...
      | '(' 'structure' Pkg_Type ')'
      | '(' 'structure' Expr ')'
Pkg_Type 
    ::= Extended_Struct_Path '.' id ['with' Pkg_Constraint {'and' Pkg_Constraint}]
Pkg_Constraint
    ::= 'type' Extended_Struct_Path '.' id Type_Equation 
Struct_Path
    ::= Module_Path
Extended_Struct_Path 
    ::= Extended_Struct_Name {'.' Extended_Struct_Name}
Extended_Struct_Name 
    ::= id {'(' [Extended_Struct_Path] ')'}
Long_Id
    ::= ...
      | Extended_Struct_Path
\end{lstlisting}






\subsection{Structures}

The motivation for structures is to package together related definitions, such as data type definitions and associated operations over that types, and to avoid running out of names. Such a package is called a {\em structure} and is introduced by the ~\lstinline!struct $\ldots$ end struct!~ construct, which contains an arbitrary sequence of definitions (their order is insignificant). A structure is also introduced by the ~\lstinline!module $\ldots$ end module!~ construct, unifying modules with structures. 

Components of a structure can be overloaded and polymorphic. 

A structure is usually given a name with the \code{structure} or \code{module} binding. 

\example A structure packaging together a type of priority queues and their operations:
\begin{lstlisting}
structure Priority_Queue is
struct
  type Priority = Number.Integer end type
  type 'a Queue is variant 
    case Empty
    case Node of Priority * 'a * 'a Queue * 'aÂ Queue
  end type
  let empty = Empty
  let rec insert queue priority element =
    match queue
    when Empty then Node(priority, element, Empty, Empty)
    when Node(p, e, left, right) then
      if priority <= p
      then Node(priority, element, (insert right p e), left)
      else Node(p, e, (insert right priority element), left)
      end if
    end match
  class Queue_is_Empty = object inherit Raiseable; end class
  let rec remove_top = function
    when Empty then raise Queue_is_Empty
    when Node(_, _, left, Empty) then left
    when Node(_, _, Empty, right) then right
    when Node(_, _, Node(l_prio, l_elt, _, _) as left, 
                    Node(r_prio, r_elt, _, _) as right) then
      if l_prio <= r_prio
      then Node(l_prio, l_elt, (remove_top left), right)
      else Node(r_prio, r_elt, left, (remove_top right))
      end if
    end function
  let extract = function
    when Empty then raise Queue_is_Empty
    when Node(priority, element, _, _) as queue then
      (priority, element, remove_top queue)
    end function
end struct
\end{lstlisting}

Outside the structure, its components can be accessed as object properties\footnote{Since everything in Aml is an object, structures are also objects.}. 

\example Accessing structure components.
\begin{lstlisting}
Priority_Queue.insert Priority_Queue.empty 1 "hello"
\end{lstlisting}






\subsection{Signatures}

Signatures are interfaces of structures. A signature specifies which components of a structure are accessible from outside of the structure, and with which type. They can be used to hide some components of a structure, like local function definitions. 

\example A signature that specifies three priority queue operations \code{empty}, \code{insert} and \code{extract}, but hides the function \code{remove_top}. Moreover, it makes the \code{Queue} type abstract, by not providing any actual representation as a concrete type. 
\begin{lstlisting}
signature A_Priority_Queue =
sig
  type Priority = Number.Integer end type (* concrete type *)
  type 'a Queue end type                  (* abstract type *)
  val empty: 'a Queue
  val insert: 'a Queue -> Number.Integer -> 'a -> 'a Queue
  val extract: 'a Queue -> Number.Integer * 'a * 'a Queue
  class Queue_is_Empty : object inherit Raiseable end object
end sig
\end{lstlisting}
Restricting the \code{Priority_Queue} structure by this signature results in another view of the \code{Priority_Queue}, where the \code{remove_top} function is not accessible and the actual representation of priority queues is hidden, using opaque ascription ``\lstinline!:>!'', where transparent ascription ``\lstinline!:!'' would not hide anything from the structure, including actual concrete type representations. 
\begin{lstlisting}
structure Abstract_Priority_Queue = 
  (Priority_Queue :> A_Priority_Queue)
\end{lstlisting}
The same restriction could also be performed during the original definition of the structure:
\begin{lstlisting}
structure Priority_Queue :> A_Priority_Queue =
struct 
  $\ldots$
end struct
\end{lstlisting}





\subsection{Functors}

Functors are functions from structures to structures. They are used to express parameterized structures: a structure $A$ is parameterized by structures $B_1 \ldots\ B_n$ is a functor $F$ with formal parameters $B_1 \ldots\ B_n$, along with the expected signatures for each $B_i$. The functor $F$ can be applied to many possible implementations of each $B_i$. 

\example A structure implementing sets as sorted lists, parameterized by a structure providing the type of the set elements and an ordering function over this type, to keep the sets sorted:
\begin{lstlisting}
type Comparison_Result = variant 
  case Less 
  case Equal
  case Greater 
end type

signature Ordered_Type =
sig
  type T end type
  val compare: T -> T -> Comparison_Result
end sig

structure Set_Functor =
functor (Element_Type: Ordered_Type) ->
  struct
    type Element = Element_Type.T end type
    type Set = Element List end type
    let empty = %[
    let rec add x s = match s
      when %[] then %[x]
      when %[head, *tail] then 
        match Element_Type.compare x head
        when Equal then s
        when Less then x :: s
        when Greater then head :: add x tail
        end match
      end match
    let rec is_member? x s = match s
      when %[] then no
      when %[head, *tail] then 
        match Element_Type.compare x head
        when Equal then yes
        when Less then no
        when Greater then is_member? x tail
        end match
      end match
  end struct
\end{lstlisting}
% TBD: more examples, descriptions





\section{Range, Floating \& Fixed Point Subtype Definitions}
\label{sec:fl-fi-subtypes}

\grammar\begin{lstlisting}
Type_Representation 
    ::= ...
      | '=' FP_Type_Def
      | '=' FP_Subtype_Def 
      | '=' FP_Range_Def
FP_Type_Def
    ::= (FiP_Type_Def | FlP_Type_Def) 
FP_Subtype_Def 
    ::= Type (FiP_Subtype_Def | FlP_Subtype_Def) 
FP_Range_Def
    ::= FP_Range
FlP_Type_Def 
    ::= FP_Digits [FP_Range]
FlP_Subtype_Def 
    ::= FP_Digits [FP_Range]
      | FP_Range
FiP_Type_Def 
    ::= FP_Delta [FP_Range] 
      | FP_Delta FP_Digits [FP_Range]
FiP_Subtype_Def
    ::= FP_Delta [FP_Digits] [FP_Range]
      | FP_Digits [FP_Range]
      | FP_Range
FP_Digits
    ::= 'digits' Expr
FP_Delta 
    ::= 'delta' Expr
FP_Range 
    ::= 'range' Expr ('..' | '..<') Expr
      | 'range' Type
\end{lstlisting}

The described syntaxes are for definitions of 4 special types of values:
\begin{enumerate}
  \item Range subtypes.
  \item Floating point types. 
  \item Ordinary fixed point types. 
  \item Decimal fixed point types. 
\end{enumerate}

\paragraph{Range subtypes}
A range subtype (the \code{FP_Range_Def} syntax category) is a type defined by a lower and upper bounds. The expected type of both bounds is \code{Magnitude}. Such a range type may be used in combination with the following subtypes to constrain them, or standalone as a regular range value. The lower bound must be lower than or equal to the upper bound. The lower bound may be negative infinity, the upper bound may be positive infinity. The range subtype itself is a subtype of \code{Magnitude}, or more precisely, a subtype of the least upper bound of types of the bounds, selecting a range of its values. For floating and fixed point types, it has to be a range of \code{Real} values. 

\paragraph{Floating point types} ~\\
A floating point type (the \code{FlP_Type_Def} syntax category) is a way to define an appropriate representation of a floating point number, based on the required accuracy instead. 
\begin{itemize}
  \item[] The {\em requested decimal precision}, which is the minimum number of significant decimal digits required for the floating point type, is specified by the value of the expression given after the keyword \code{digits}. Such expression is expected to be of \code{Integer} type. 
  \item[] The bounds of the range specification are expected to be \code{Real} type; the types do not need to be the same.\footnote{E.g., one bound can be an integer, the other a real number.}
  \item[] The requested decimal precision shall be positive and not greater than an implementation-defined precision limit in \code{Number.Max_Base_Digits}. If the range specification is omitted, the requested decimal precision shall be not greater than \code{Number.Max_Digits}. 
  \item[] A floating point type definition is illegal if the implementation does not support a floating point type that satisfies the requested decimal precision and range. 
  \item[] A subtype of a floating point type is compatible to the parent type if the digits of the subtype are not greater than the digits of the parent type, and its range fits to the range of the parent type. 
\end{itemize}

\example Examples of floating point types and subtypes:
\begin{lstlisting}
type Coefficient is digits 10 range -1.0 ..< 1.0 
end type

type Mass is digits 7 range 0.0 ..< 1.0e+35 
end type

(* a subtype with a smaller range *)
type Probability is Real range 0.0 ... 1.0
end type
\end{lstlisting}

\paragraph{Fixed point types}
An ordinary fixed point type (the first branch of the \code{FiP_Type_Def} syntax category) is a way to define a decimal type, based on the given delta. 
A decimal fixed point type (the second branch of the \code{FiP_Type_Def} syntax category) is a way to define a decimal type, based on the given delta and number of needed digits.  
\begin{itemize}
  \item[] The error bound of a fixed point type is specified as an absolute value, called the {\em delta} of the fixed point type. 
  \item[] For a type defined by the fixed point type definition, the delta of the type is specified by the value of the expression given after the keyword \code{delta}; this expression is expected to be of a \code{Real} type. For a type defined by the decimal fixed point definition, the number of significant decimal digits is specified by the expression given after the keyword \code{digits}; this expression is expected to be of \code{Integer} type. 
  \item[] The expressions given after the reserved keywords \code{delta} and \code{digits} shall result in positive values. 
  \item[] The set of values of a fixed point type comprise the integral multiples of a number called the {\em small} of the type. 
  \item[] For ordinary fixed point type, the small is an implementation-defined power of 2 not greater than the delta, unless annotation ~\lstinline!@[fixed_point_small $s$]! is applied to the type, defining the small to be $s$, where $s$ is not greater than the delta. 
  \item[] For decimal fixed point type, the small equals the delta; the delta shall be a power of 10. If a range specification is given, both bounds of the range shall be in the range $-(10^{\digits}-1)*\fpdelta$ \code{...} $(10^{\digits}-1)*\fpdelta$. 
  \item[] A fixed point type definition is illegal if the implementation does not support a fixed point type with the given small and specified range or digits. 
  \item[] An ordinary fixed point type definition defines an ordinary fixed point type, whose base range includes at least all multiples of the small that are between the bounds defined by the range specification, if it is given, or between negative infinity to positive infinity\footnote{Using the \code{Decimal} types.}, if the range specification is not given.
  \item[] A decimal fixed point type definition defined a decimal fixed point type, whose base range includes at least the range $-(10^{\digits}-1)*\fpdelta$ \code{...} $(10^{\digits}-1)*\fpdelta$. 
  \item[] If a decimal fixed point type definition does is not given a range specification, then an implicit range $-(10^{\digits}-1)*\fpdelta$ \code{...} $(10^{\digits}-1)*\fpdelta$ is specified for it. 
  \item[] A subtype of a decimal fixed point type is compatible to the parent type if the digits of the subtype are not greater than the digits of the parent type, and its range fits to the range of the parent type. 
\end{itemize}

\example Examples of fixed point types and subtypes:
\begin{lstlisting}
type Volt is delta 0.125 range 0.0 ..< 255.0 
end type

type Fraction is delta Number.Fine_Delta range -1.0 ..< 1.0 
end type

type Money is delta 0.01 digits 15 
end type

type Salary is Money digits 10 
end type
\end{lstlisting}





\section{Type Classes \& Type Families}
\label{sec:type-classes}

\grammar\begin{lstlisting}
Typedef
    ::= ...
      | Type_Class_Dcl
      | Type_Family_Dcl
      | Type_Instance_Def
Type_Class_Dcl
    ::= 'class' [Type_Context] id [Type_Param_Clause] [Given_Clauses] 
        Type_Class_Dcl_Body
      | 'class' [Type_Context] Prefix_Type_Param_Clause id [Given_Clauses] 
        Type_Class_Dcl_Body
Type_Family_Dcl
    ::= 'family' [Type_Context] id [Type_Param_Clause] [Given_Clauses]
        [Type_Family_Closed_Def] 
      | 'family' [Type_Context] Prefix_Type_Param_Clause id [Given_Clauses] 
        [Type_Family_Closed_Def] 
Type_Instance_Def
    ::= Type_Class_Instance_Def
      | Type_Family_Instance_Def
      
Type_Class_Dcl_Body 
    ::= 'with' [Type_Class_Spec {'and' Type_Class_Spec}]
      | Type_Equation
Type_Class_Spec
    ::= Struct_Spec
      | Struct_Def  (* to define "default" implementations if instance omits it *)
Type_Class_Instance_Def
    ::= ['class'] 'instance' [Type_Context] 
        Type_Instance_Id [Given_Clauses] 
        Type_Class_Instance_Body
Type_Class_Instance_Body 
    ::= 'with' Template_Stat {'and' Template_Stat}
      | Type_Equation
      
Type_Family_Closed_Def
    ::= 'with' [Type_Family_Instance_Def {'and' Type_Family_Instance_Def}]
      | Type_Equation
Type_Family_Instance_Def
    ::= ['family'] 'instance' [Type_Context] 
        Type_Instance_Id [Given_Clauses] 
        Type_Family_Instance_Body
Type_Family_Instance_Body
    ::= Type_Equation
      | Variant_Type_Def

Type_Instance_Id
    ::= id Type_Args
      | Type_Arg id
\end{lstlisting}



























